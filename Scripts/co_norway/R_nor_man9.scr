// Guard - inside crew's quarters

Block
{
frame see; 		FRM_FindFrame(see, "dummy_turn_man9");
Frame m_dvo03;	FRM_FindFrame(m_dvo03, "m_dvo03");

HUMAN_SETSIGHTRANGE(16);

SetAlarmType(1,0);		// close shooting
SetAlarmType(2,0);		// enemy shots
SetAlarmType(4,0);		// hear steps
SetAlarmType(8,0);
SetAlarmType(16,0);
SetAlarmType(32,1);		// sighted
SetAlarmType(64,1);		// is injured
SetAlarmType(128,0);
SetAlarmType(256,0);	// friendly shots
SetAlarmType(512,0);

integer AIEvent;
integer way_predictor;

HUMAN_SETMODE_Stand();
HUMAN_SETMODE_Guard();
}

HUMAN_Suspend(true);
goto end;

OnSignal(1)
{
SetAlarmType(4,1);
goto guard;
}

Whenever s(_PlayerInRange(30))
{
HUMAN_Suspend(0);
goto guard;
}

OnAlarm()
{
block
{
DisableSignals(1);
AIEvent =_GetAlarmType(); 
HUMAN_SETAIMODE_Aggressive();
}
HUMAN_SetAnim("", 200, 200, 0);
EndScript();
}

OnAlarmDone()
{
DisableSignals(0);
HUMAN_SetAlarm(0);
SetAlarmType(1023,0);
SetAlarmType(100,1);
goto after;
}

OnDeath()
{
DisableSignals(1);
EndScript();
}

Whenever kokot(_SignalReceived(22))			//Sirena on
{
goto after;
}

Whenever AlarmsOff (_LoadGameValue(16)==1)
{
DisableAlarms(1);
goto end;
}

label guard:
  way_predictor = _RandomInt(3) + 1;
  if (way_predictor == 1) 
     { 
        HUMAN_Move("man9_1");
        delay(3000);
     }
  if (way_predictor == 2) 
     {  
        HUMAN_Move("man9_2");
        delay(250);
        HUMAN_TurnAt(see);
        delay(3000);
     }
  if (way_predictor == 3) 
     {  
        HUMAN_Move("man9_3");
        delay(250);
        HUMAN_TurnAt(see);
        delay(3000);
     }
goto guard;

label after:	
	HUMAN_SETMODE_Walk();
	HUMAN_WeaponOnArm(1);
	HUMAN_Move("small9snip");
	HUMAN_SETMODE_Crouch();	
	HUMAN_TurnAt(m_dvo03);
	HUMAN_SetSniper(1,5);

label end: