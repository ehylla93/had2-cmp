//coop only


block
{
SetAlarmType(1,1); //blizka strelba
SetAlarmType(2,1); //slysim strelbu
SetAlarmType(4,1); //kroky
SetAlarmType(8,0); //zvuky by use
SetAlarmType(16,1); //exploze
SetAlarmType(32,1); //vidi
SetAlarmType(64,1); //jsem zranen
SetAlarmType(128,1); //slysim smrtelny skrek
SetAlarmType(256,1); //pratelska strelba
SetAlarmType(512,1); //vidim flakotu

HUMAN_SETAIMODE_Defensive();
HUMAN_SETMODE_Guard();
HUMAN_SETSIGHTRANGE(40);
HUMAN_SETHEARRANGE(180);


integer suspend;
Integer endx;		endx=0;
integer aievent;
integer a;
integer k;
integer d;
integer alarmpos;	alarmpos=0;
integer mluvi;

frame detect1;		FRM_FindFrame (detect1,"detect_zona2");
frame cum1;		FRM_FindFrame(cum1, "z2flak1cum1");
frame cum2;		FRM_FindFrame(cum2, "z2flak1cum2");
Frame flak1;		FRM_FindFrame(flak1, "z2_flak1");
Frame z2flakkecani;	FRM_FindFrame(z2flakkecani, "z2flakkecani");
Frame z2_flak1;	FRM_FindFrame(z2_flak1, "z2_flak1");

frame me;		FRM_GetMyFrame(me);
FRM_SwitchFaceTexture(me, "e_f039");
}

//---------------------------------------------------------
if ((_LoadGameValue(3)==0))
{
DisableWhenevers(1);
DisableSignals(1);
HUMAN_Suspend(1);
goto end;
}
//---------------------------------------------------------

HUMAN_Suspend(1);
suspend=1;

goto end;

//---------------------------------------------------------------------------------------------------

Whenever go(_SignalReceived(1))
{
//detektor hracu
HUMAN_Suspend(0);
mluvi=1;
SetAlarmType(1023,0);
SetAlarmType(1011,1);	//bez 4  a use
HUMAN_SetAnim("%%mluvi6", 500,500,1);
goto end;
}


//---------------------------------------------------------------------------------------------------------

OnSignal(2)
{
//od kecatoru

label kecej:

k=(_RandomInt(5));
d=(_RandomInt(4000));
d=(5000+d);

if (k==0)
{
HUMAN_SetAnim("%%mluvi3",900,900,1);
delay (1028);
goto kecej;
}

if (k==1)
{
HUMAN_SetAnim("%%mluvi4",900,900,1);
delay (1285);
goto kecej;
}

if (k==2)
{
HUMAN_SetAnim("%%mluvi5",900,900,0);
delay (1914);
goto kecej;
}

if (k==3)
{
HUMAN_SetAnim("%%mluvi6",900,900,1);
delay (d);
goto kecej;
}

if (k==4)
{
HUMAN_SetAnim("%%mluvi2",900,900,1);
delay (d);
goto kecej;
}

}
//---------------------------

OnSignal(3)
{
HUMAN_SetAnim("",300,300,0);
goto end;
}

//-----------------------------

OnSignal(4)
{
SetWhenever (alarm,0);
SetAlarmType(4,1);
goto chodim;
}

Whenever alarm(_SignalReceived(5))
{
HUMAN_SetAnim("", 90, 90, 0);
block
{
SetAlarmType(4,1);
HUMAN_SETSIGHTRANGE(40);
HUMAN_WeaponOnArm(1);	
}	


HUMAN_TurnAtNearestPlayer();
goto end;
}
//-----------------------------

label chodim:

a=1;
HUMAN_Move("z2_flak2_01");
a=2;
HUMAN_SetAnim("%%kouka", 300, 300, 0);
delay (4285);
HUMAN_SetAnim("", 300, 300, 0);
label 2:
HUMAN_Move("z2_flak2_02");
a=3;
HUMAN_SetAnim("%%kouka", 300, 300, 0);
delay (4285);
HUMAN_SetAnim("", 300, 300, 0);
label 3:
HUMAN_Move("z2_flak2_03");
a=4;
HUMAN_SetAnim("%%kouka", 300, 300, 0);
delay (4285);
HUMAN_SetAnim("", 300, 300, 0);
label 4:
HUMAN_Move("z2_flak2_02");
a=1;
HUMAN_SetAnim("%%kouka", 300, 300, 0);
delay (4285);
HUMAN_SetAnim("", 300, 300, 0);
goto chodim;


//---------------------------------------------------------------------------------------------------------
label rozdel:
if (a==1)	{ goto chodim; }
if (a==2)	{ goto 2; }
if (a==3)	{ goto 3; }
if (a==4)	{ goto 4; }


goto end;


//---------------------------------------------------------------------------------------------------------
OnAlarm()
{
block
{
DisableWhenevers(1);
DisableSignals(1);
SetAlarmType(1015,1);
aievent=(_GetAlarmType());
PrintfNumber("z2_flak2 alrm:",aievent);
SendSignal (z2flakkecani,1);
SendSignal (z2_flak1,2);
}



if ((((aievent==1) or (aievent==32)) or (aievent==64)) or (endx==1))

	{
	block
	{
	HUMAN_SetAnim("", 100, 100, 0);
	endx=1;
	}
	EndScript();
	}


if (((aievent==2) or (aievent==16)) or (aievent==256))

{


if (_LoadGameValue(6)==0)
	
	{
	HUMAN_SetAlarm(0);

	goto end;
	}
block
{
HUMAN_SetAnim("", 100, 100, 0);
endx=1;
EndScript();
}

}


if (aievent==4)
{

SetAlarmType(4,0);
HUMAN_SetAlarm(0);
HUMAN_TurnAtNearestPlayer();
delay (2000);
SetAlarmType(4,1);

if (mluvi==1)
{
HUMAN_TurnAt(flak1);
goto end;
}

goto rozdel;
}


block
{
HUMAN_SetAnim("", 100, 100, 0);
endx=1;
}

EndScript();

}


//---------------------------------------------------------------------------------------------------------


OnAlarmDone()
{



SetAlarmType(1023,0);
SetAlarmType(1015,1);
HUMAN_SetAlarm(0);
HUMAN_Reload();
HUMAN_SETMODE_Crouch();
goto end;
}

OnDeath()
{
block
{
SendSignal (z2flakkecani,1);
DisableWhenevers(1);
DisableSignals(1);

}
EndScript();
goto end;
}




label end:
