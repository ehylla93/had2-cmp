// N32 - shooting from the cannon
// -------------------------------------------------------------------------------------------------------------
// Variables definition
Block
{
Integer aievent = 0;
Frame ruka;					FRM_FindFrame(ruka, "N32.hw1");
Frame kanon01xplo;			FRM_FindFrame(kanon01xplo, "N_kanon01xplo");
Frame naboj_vkladanie;		FRM_FindFrame(naboj_vkladanie, "nabojN32_01_vkladanie");
Frame naboj_vkladanie_v;	FRM_FindFrame(naboj_vkladanie_v, "nabojN32_01_vkladanie.Cylinder01");
Frame naboj_nesenie;		FRM_FindFrame(naboj_nesenie, "nabojN32_01_nesenie");
Frame naboj_namieste;		FRM_FindFrame(naboj_namieste, "nabojN32_01_namieste");
Frame naboj_kanon;			FRM_FindFrame(naboj_kanon, "nabojN32_01_kanon");
Frame ucpavka_nesenie;		FRM_FindFrame(ucpavka_nesenie, "ucpavkaN32_01_nesenie");
Frame ucpavka_nesenie_v;	FRM_FindFrame(ucpavka_nesenie_v, "ucpavkaN32_01_nesenie.Object01");
Frame ucpavka_namieste;		FRM_FindFrame(ucpavka_namieste, "ucpavkaN32_01_namieste");
Frame ucpavka_kanon;		FRM_FindFrame(ucpavka_kanon, "ucpavkaN32_01_kanon");
Frame turn;		FRM_FindFrame(turn, "N32_turn");
Frame zbran;	FRM_FindFrame(zbran, "N32_zbran");
Frame me;		FRM_GetMyFrame(me);
Integer alarmed;	alarmed=0;
Integer hold;		hold=0;	
Frame N22;		FRM_FindFrame(N22, "N22");

SetAlarmType(156, false);
//SetAlarmType(1023, false);
}

// -------------------------------------------------------------------------------------------------------------
// Whenever keywords and signals
OnAlarm()
{
	HUMAN_SetAnim("", 50, 50, false);
	If(hold==1)
	{
		Block
		{
		FRM_CreatePhysicalObject(naboj_vkladanie_v, 15, false, true);
		FRM_SetOn(naboj_vkladanie, false);
		FRM_SetOn(naboj_nesenie, false);
		}
	}
	If(hold==2)
	{
		Block
		{
		FRM_CreatePhysicalObject(ucpavka_nesenie_v, 5, false, true);
		FRM_SetOn(ucpavka_nesenie, false);
		}
	}
	Block
	{
		aievent=_GetAlarmType();
		alarmed=1;
	HUMAN_SETMODE_Run();
	SendSignal(N22, 1);
	}
	HUMAN_Move("N32_4");
	SendSignal(zbran, 1);
	HUMAN_CreateItemInStore(4, 3);

	If((aievent==1) OR (aievent==512))
	{
		HUMAN_SETMODE_Crouch();
		EndScript();
	}

	EndScript();
}

OnAlarmDone()
{
	HUMAN_SetAlarm(false);
	HUMAN_SETMODE_Stand();
	HUMAN_SetOptimized(1);
	GoTo action;
}

OnDeath()
{
If(alarmed==0)
{
	If(hold==1)
	{
		Block
		{
		FRM_CreatePhysicalObject(naboj_vkladanie_v, 15, false, true);
		FRM_SetOn(naboj_vkladanie, false);
		FRM_SetOn(naboj_nesenie, false);
		}
	}
	If(hold==2)
	{
		Block
		{
		FRM_CreatePhysicalObject(ucpavka_nesenie_v, 5, false, true);
		FRM_SetOn(ucpavka_nesenie, false);
		}
	}
}
	EndScript();
}

Whenever activate(_SignalReceived(1))	// received from N31_A1, N31_A2, N31_A3
{
	HUMAN_Suspend(false);
	Block
	{
	FRM_TeleportNearFrame(naboj_nesenie, ruka, 1);	
	FRM_TeleportNearFrame(naboj_vkladanie, ruka, 1);
	FRM_TeleportNearFrame(ucpavka_nesenie, ruka, 1);
	SendSignal(kanon01xplo, 4);
	}
	FRM_FindFrame(ruka, "");
	GoTo action;
}

OnSignal(2)	// received from N31
{
	HUMAN_SetAnim("%%neslysim", 200, 200, false);
	Delay(2300);
	GoTo end;
}

OnSignal(3)	// received from N_kanon01xplo
{
	Block
	{
	FRM_SetOn(naboj_kanon, false);
	FRM_SetOn(ucpavka_kanon, false);
	}
	SendSignal(kanon01xplo, 4);
	Delay(1000);
	GoTo action;
}

Whenever AlarmsOff (_SignalReceived(19))
{
DisableAlarms(1);
goto end;
}

// -------------------------------------------------------------------------------------------------------------
Block
{
HUMAN_SETAIMODE_Defensive();
FRM_SwitchFaceTexture(me, "e_f053");
FRM_SetOn(naboj_nesenie, false);
FRM_SetOn(naboj_vkladanie, false);
FRM_SetOn(naboj_kanon, false);
FRM_SetOn(ucpavka_nesenie, false);
FRM_SetOn(ucpavka_kanon, false);
}
HUMAN_Suspend(true);
GoTo end;

Label action:
	HUMAN_Move("N32_1");
	Block
	{
	HUMAN_SETMODE_Guard();
	FRM_SetOn(naboj_nesenie, true);	// na nesenie
	hold=1;
	FRM_SetOn(naboj_namieste, false);	// na mieste
	}
	HUMAN_SetAnim("%%nesenaboj", 50, 50, true);
	HUMAN_Move("N32_2");
	HUMAN_SetAnim("", 50, 50, false);
	HUMAN_TurnAt(turn);
	Block
	{
	FRM_SetOn(naboj_nesenie, false);	// na nesenie
	FRM_SetOn(naboj_vkladanie, true);	// na vkladanie
	}
	HUMAN_SetAnim("%%delistanaboj", 50, 200, false);
	Delay(1400);
	Block
	{
	FRM_SetOn(naboj_vkladanie, false);
	FRM_SetOn(naboj_kanon, true);
	hold=0;
	}
	Delay(4100);
	HUMAN_SETMODE_Walk();
	HUMAN_Move("N32_3");
	Block
	{
	FRM_SetOn(ucpavka_nesenie, true);
	hold=2;
	FRM_SetOn(ucpavka_namieste, false);
	}
	HUMAN_SETMODE_Guard();
	HUMAN_SetAnim("%%neseucp", 50, 50, true);
	HUMAN_Move("N32_2");
	HUMAN_SetAnim("", 50, 50, false);
	HUMAN_TurnAt(turn);
	HUMAN_SetAnim("%%delistaucp", 50, 50, false);
	Delay(1700);
	HUMAN_SETMODE_Walk();
	Block
	{
	FRM_SetOn(ucpavka_kanon, true);
	FRM_SetOn(ucpavka_nesenie, false);
	hold=0;
	}
	Delay(1000);
	SendSignal(kanon01xplo, 2);
	Delay(2000);
	HUMAN_Move("N32_1");
	GoTo end;

Label end: