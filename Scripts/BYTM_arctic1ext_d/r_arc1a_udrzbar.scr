//Soldier in generator building

Block 
{
integer AIEvent = 0;
integer AI_OK = 0;
frame board;		FRM_FindFrame(board, "m_AF5_archiv_");
frame door;			FRM_FindFrame(door, "m_dv_st");
frame see;			FRM_FindFrame(see, "dummy_udrzbar_see");
frame AllKilled;	FRM_FindFrame(AllKilled, "dummy_OBJ1_sender");

HUMAN_SETAIMODE_Zombie();
HUMAN_SetOptimized(1);
HUMAN_SETMODE_Walk();

SetAlarmType(1023, False);
}

HUMAN_SETMODE_Stand();
HUMAN_Suspend(True);				
goto TheEND;

//---------------------------------------------------------------------------------------------------

OnAlarm()
{
  DisableSignals(true);
  AIEvent =_GetAlarmType(); 
  PrintfNumber("Alarm Gen Guard: ", AIEvent);
  If (AI_OK == 1)
    {
       SetAlarmType(127, True);		// 64 + 32 + 16 + 8 + 4 + 2 + 1
       EndScript();
    }
  HUMAN_Attack(door, 1000);
  HUMAN_SETMODE_Run();
  HUMAN_Move("udrzbar_2");
  HUMAN_SETMODE_Walk();
  HUMAN_Move("udrzbar_3");
  HUMAN_SETAIMODE_Defensive();
  HUMAN_TurnAt(see);
  HUMAN_SETMODE_Crouch();
  Delay(800);
  AI_OK = 1;
  HUMAN_SetAlarm(False);
  goto TheEND;
}

OnAlarmDone()
{
  SetAlarmType(1023, False);
  SetAlarmType(99, True);		// 64 + 32 + 2 + 1
  HUMAN_SetAlarm(False);
  HUMAN_SetOptimized(2);
  HUMAN_SETMODE_Walk();
  HUMAN_SETMODE_Stand();
  HUMAN_Move("udrzbar_1d");
  HUMAN_SETMODE_Crouch();
  HUMAN_TurnAt(door);
  Delay(800);
  goto TheEND;
}

OnDeath()
{
  SendSignal(AllKilled, 30);
  EndScript();
}

//---------------------------------------------------------------------------------------------------

OnSignal(1)	
{
HUMAN_Suspend(False);
HUMAN_Move("udrzbar_1a");
HUMAN_TurnAt(door);
Delay(500);
}

OnSignal(2)
{
SetAlarmType(544, True);
HUMAN_Attack(door, 5000);
}

Whenever AlarmsOff (_LoadGameValue(16)==1)
{
DisableAlarms(1);
}

//---------------------------------------------------------------------------------------------------

Label TheEND: