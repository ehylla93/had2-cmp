// BU2_Allied01 - allied soldier, shooting at BU2_R23, BU2_R12
// player in range 20 - shoot and continue to next checkpoint
// player in range 21-40 stop and shoot
// player in range > 41 - go and die somewhere
// -------------------------------------------------------------------------------------------------------------
// Variables definition
Block
{
Frame r12;			FRM_FindFrame(r12, "BU2_R12");
Frame r23;			FRM_FindFrame(r23, "BU2_R23");
Frame shoot01;		FRM_FindFrame(shoot01, "BU2_A01_shoot01");
Frame bombstart;	FRM_FindFrame(bombstart, "BU2_bomb_start");
Frame me;			FRM_GetMyFrame(me);

Integer path;		path=0;
Integer r12live;	r12live=0;
Integer r23live;	r23live=0;

DisableWhenevers(true);
HUMAN_SETEVENTS(false);
FRM_SwitchFaceTexture(me, "e_f054");

SetAlarmType(1023, false);
}

HUMAN_SETMODE_Run();

goto end;

// -------------------------------------------------------------------------------------------------------------
// Whenever keywords and signals

Whenever player(_PlayerInRange(20))	// in range 20
{
	HUMAN_WeaponOnArm(0);
	Delay(500);
	HUMAN_SetAnim("%%moveondrep", 200, 200, false);
	Delay(2200);
	HUMAN_WeaponOnArm(1);
	Delay(500);
	HUMAN_SETMODE_Stand();
	SetWhenever(player2, true);
	SetWhenever(player3, true);
	If(path==2)	{	GoTo continue2;	}
	If(path==3)	{	GoTo continue3;	}
	If(path==4)	{	GoTo continue4;	}
	GoTo continue2;
}

Whenever player2((!_PlayerInRange(21)) AND (_PlayerInRange(40)))	// in range 21-40
{
	HUMAN_Stop();
	HUMAN_TurnAt(shoot01);
	HUMAN_SETMODE_Crouch();
	SetWhenever(player, true);
	SetWhenever(player3, true);
	GoTo shoot;
}

Whenever player3(!_PlayerInRange(41))	// in range 40+
{
	HUMAN_SETMODE_Stand();
	SetWhenever(player, true);
	SetWhenever(player2, true);
	If(path==2)	{	GoTo continue2;	}
	If(path==3)	{	GoTo continue3;	}
	If(path==4)	{	GoTo continue4;	}
	GoTo continue2;
}

Whenever bu2_r23(_FrameInRange(r23)<30)
{
	SendSignal(r23, 1);
	HUMAN_Stop();
	HUMAN_TurnAt(r23);
	HUMAN_SETMODE_Crouch();
	GoTo meet_r23;
}

Whenever bu2_r12(_FrameInRange(r12)<30)
{
	SendSignal(r12, 1);
	HUMAN_Stop();
	HUMAN_TurnAt(r12);
	HUMAN_SETMODE_Crouch();
	GoTo meet_r12;
}

OnSignal(1)
{
Delay(5000);
DisableWhenevers(false);
SetWhenever(player, false);
SetWhenever(player2, false);
goto beh;
}

// -------------------------------------------------------------------------------------------------------------

Label beh:
	Delay(1501);
	SendSignal(bombstart, 1);
	HUMAN_SetOptimized(1);
	HUMAN_Move("BU2_A01_01");
		path=2;
	GoTo shootonce;
Label continue2:
	HUMAN_SetOptimized(1);
	HUMAN_Move("BU2_A01_02");
	SetWhenever(player2, true);
		path=3;
	GoTo shootonce;
Label continue3:
	HUMAN_SetOptimized(1);
	HUMAN_Move("BU2_A01_03");
		path=4;
	GoTo shootonce;
Label continue4:
	HUMAN_SetOptimized(1);
	HUMAN_Move("BU2_A01_04");
	HUMAN_Kill(me);
	GoTo end;

Label shoot:
	HUMAN_Attack(shoot01, 3000);
	Delay(2000);
	GoTo shoot;

Label shootonce:
	HUMAN_SetOptimized(2);
	HUMAN_TurnAt(shoot01);
	HUMAN_SETMODE_Crouch();
	HUMAN_Attack(shoot01, 2000);
	Delay(2000);
	HUMAN_SETMODE_Stand();
	If(path==2)	{	GoTo continue2;	}
	If(path==3)	{	GoTo continue3;	}
	If(path==4)	{	GoTo continue4;	}
	GoTo continue2;

Label meet_r23:
	HUMAN_SetOptimized(2);
		r23live=_ACTOR_GetState(r23);
	If(r23live==1)
	{
		HUMAN_Attack(r23, 2000);
		Delay(2000);
		GoTo meet_r23;
	}
	If(path==2)	{	GoTo continue2;	}
	If(path==3)	{	GoTo continue3;	}
	If(path==4)	{	GoTo continue4;	}
	GoTo continue2;

Label meet_r12:
	HUMAN_SetOptimized(2);
		r12live=_ACTOR_GetState(r12);
	If(r12live==1)
	{
		HUMAN_Attack(r12, 2000);
		Delay(2000);
		GoTo meet_r12;
	}
	If(path==2)	{	GoTo continue2;	}
	If(path==3)	{	GoTo continue3;	}
	If(path==4)	{	GoTo continue4;	}
	GoTo continue2;

Label end: