// cetnik01

Block
{
Frame tree;		FRM_FindFrame(tree,"e_tree196");
Frame cetnik1;	FRM_FindFrame(cetnik1,"cetnik_");
Frame auto;		FRM_FindFrame(auto,"blitz1");
Frame driver;	FRM_FindFrame(driver,"ridic nakladaku");
Frame speech;	FRM_FindFrame(speech, "speech1");
Frame me;		FRM_GetMyFrame(me);
Frame posila4;	FRM_FindFrame(posila4, "posila4");
Frame posila6;	FRM_FindFrame(posila6, "posila6");
Integer kur=0;
Integer ctr=0;

HUMAN_SETMODE_Guard();
HUMAN_SETAIMODE_Aggressive();
FRM_SwitchFaceTexture(me, "e_f053");
}

goto end;

OnSignal(1)	// received from detektorauta
{
	HUMAN_SetAnim("", 500, 1500, false);
	HUMAN_TurnAt(auto);
	GoTo end;
}

OnSignal(2)	// received from ridic nakladaku
{
	HUMAN_SetOptimized(1);
	HUMAN_Move("sloup");
	HUMAN_Move("zadkolo");
	HUMAN_TurnAt(auto);
	Delay(1000);
	HUMAN_Move("zadekauta");
	HUMAN_TurnAt(auto);
	Delay(100);
	HUMAN_SetAnim("%%kontrolaauta", 500, 500, false);
	Delay(13000);
	HUMAN_Move("zadkolo2");
	HUMAN_SETSIGHTRANGE(15);
	HUMAN_Move("zastavit");
	HUMAN_TurnAt(auto);
	Delay(100);
	HUMAN_SetAnim("%%kontrolaauta", 500, 500, false);
	Delay(13000);
	HUMAN_Move("predek");
	HUMAN_TurnAt(auto);
	Delay(100);
	HUMAN_SetAnim("%%kontrolaauta", 500, 500, false);
	Delay(13000);
	HUMAN_Move("pozdrav");
	HUMAN_TurnAt(auto);
	HUMAN_SetAnim("!salutovani", 500, 500, 0);
	Delay(2500);
	HUMAN_SetAnim("", 500, 500, 0);	
	SendSignal(driver, 1);
	SendSignal(cetnik1, 2);
	HUMAN_Move("straze2");
	HUMAN_SetOptimized(2);
	SendSignal(cetnik1, 3);
	HUMAN_TurnAt(cetnik1);
	SendSignal(speech, 1);
	Delay(100);
	GoTo hlidka;
}

OnSignal(3)	// received from speech1
{
	HUMAN_Stop();
	GoTo end;
}

OnAlarm()
{
	HUMAN_Stop();
	Block
	{
	SendSignal(speech, 2);
	FRM_MorphSpeech(me, 0, 0, 0);
	DisableSignals(true);
	HUMAN_SETMODE_Run();
	HUMAN_SETSIGHTRANGE(80);
	}
	HUMAN_SetAnim("", 100, 100, false);
	HUMAN_SETMODE_Crouch();
	EndScript();
}

OnAlarmDone()
{
	Block
	{
	HUMAN_SetAlarm(false);
	DisableSignals(false);
	HUMAN_SetOptimized(1);
	}
	HUMAN_WeaponOnArm(false);
	HUMAN_SETMODE_Stand();
	HUMAN_Move("straze2");
	HUMAN_SetOptimized(2);
	GoTo hlidka;
}

OnDeath()
{
	Block
	{
	SendSignal(speech, 2);
	FRM_MorphSpeech(me, 0, 0, 0);
	SendSignal(posila4, 2);
	SendSignal(posila6, 2);
	}
	EndScript();
}

OnSignal(31)	// received from transformator_1
{
	HUMAN_Stop();
	Block
	{
	HUMAN_SetAnim("", 100, 100, false);
	HUMAN_SETMODE_Walk();
	FRM_MorphSpeech(me, 0, 0, 0);
	SendSignal(speech, 2);
	HUMAN_SetOptimized(1);
	}
	HUMAN_Move("cetnik01_01");
	Delay(2000);
	HUMAN_Move("straze2");
	HUMAN_SetOptimized(2);
	HUMAN_TurnAt(cetnik1);
	SendSignal(cetnik1, 3);
	SendSignal(speech, 1);
	Delay(100);
	GoTo hlidka;
}

Whenever AlarmsOff (_SignalReceived(19))
{
DisableAlarms(1);
}

Label hlidka:
	HUMAN_SETSIGHTRANGE(28);
	Delay(21000);	
	Integer point=_RandomInt(5);
	If(point==0)
	{
		HUMAN_TurnAt(tree);
		HUMAN_SetAnim("(ukazovani", 500, 500, false);
		Delay(2000);
		HUMAN_SetAnim("", 200, 200, 0);
	}
	Delay(5000);
	HUMAN_TurnAt(cetnik1);
	Integer kecani=_RandomInt(3);
	If(kecani==0)
	{
		HUMAN_SetAnim("%%kecy1", 500, 1500, false);
		Delay(12000);
	}
	kur=_RandomInt(5);
	If(kur==1)
	{
		Gosub cigaro;
	}
	GoTo hlidka;

Label cigaro:
Delay(4500);
HUMAN_SetAnim("%(koureniB", 1000, 1000, 0);		//(Looking Through Pockets Then Lights Fag)
Delay(10000);
HUMAN_SetAnim("%%cumvoda", 1000, 1000, 0);
Delay(5000);
Label SMOKE2:
ctr=ctr+1;
if(ctr==4)
{
ctr=0;
Return;
}
HUMAN_SetAnim("%(koureni5", 1000, 1000, 0);		//(Brings Hand Up To Smoke)
Delay(10000);
goto SMOKE2;

Label end: