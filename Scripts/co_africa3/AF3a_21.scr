// AF3a_21 - mechanic repairing the Opel
// -------------------------------------------------------------------------------------------------------------

Block{
  FRAME this;			FRM_GetMyFrame(this);
  FRAME opel1;  		FRM_FindFrame(opel1, "La_OpelAf2.Cylinder02");
  FRAME opel2;  		FRM_FindFrame(opel2, "dummy_wheelrepair");
  FRAME chladic2; 		FRM_FindFrame(chladic2, "dummy_opel2_chladic2");
  FRAME flaska;			FRM_FindFrame(flaska, "");
  FRAME player;			FRM_FindFrame(player, "");
  FRAME obj1;			FRM_FindFrame(obj1, "AF3a_obj");
  FRAME dab;			FRM_FindFrame(dab, "dummy_mechanik_dabind");
  INTEGER rnd			= 0;
  INTEGER Atype 		= 0;
  INTEGER below_car		= 0;
  
  SaveGameValue(62, 0);		// jestli muze mechanik kecat

  SetAlarmType(1023, false);		// all alarms offf
  SetAlarmType(323, false);		// 256, 64, 2 on
}
 
// -------------------------------------------------------------------------------------------------------------

Whenever nadohled (_PlayerInRange(80))
{
  HUMAN_Suspend(false);
  goto ACTIVITY;
}

OnDeath(){
  PlayMusic(34);
  SaveGameValue(62, 0);
  SetWhenever(nadohled, false);
  FRM_MorphSpeech(this, 0, 0, 0);
  FRM_MorphSpeech(player, 0, 0, 0);
  SUBTITLES_SetOn(true);
  SUBTITLES_SetText(07991408);		//"OBJECTIVE FAILED: MECHANIC KILLED."
  Delay(3500);
  SUBTITLES_SetOn(false);
  SetObjectiveStatus(4, 3);
  EndScript();
}

OnAlarm(){
  block
  {
    SaveGameValue(62, 0);
    HUMAN_SetAnim("", 300, 100, false);
    HUMAN_Stop();
    HUMAN_SETSIGHTRANGE(200);
    Atype = _GetAlarmType();
    SUBTITLES_SetOn(true);
  }

  if((Atype == 256) OR (Atype == 2))
  {
    SetAlarmType(258, false);
    HUMAN_SetAnim("%%panika", 800, 800, true);
    HUMAN_SetAlarm(false);
    goto END;
  }

  if(Atype == 1){
    HUMAN_Stop();
    SetAlarmType(1, false);
    HUMAN_SetAnim("", 200, 200, false);
    Delay(200);
    HUMAN_SetAnim("%%panika", 500, 200, true);
    Delay(200);
    HUMAN_SetAlarm(false);
    goto END;
  }

  if(Atype == 64){
   below_car = 1;
   HUMAN_Stop();
   SetAlarmType(64, false);
   HUMAN_SetAnim("", 200, 200, false);
   Delay(200);
   HUMAN_SETMODE_Run();
   HUMAN_Move("AF3a_21_01");  
   Delay(100);   
   HUMAN_SETMODE_Walk();
   HUMAN_SetOptimized(1);
   HUMAN_SETMODE_Lie();	
   HUMAN_Move("AF3a_21_bcar");
    goto END;
  }
  HUMAN_SetAlarm(false);
goto ACTIVITY;
}

OnAlarmDone(){ 
  SetAlarmType(512, false);
  HUMAN_SETSIGHTRANGE(80);
  HUMAN_SetOptimized(1);
  HUMAN_SetAlarm(false);
  goto END;
}

Whenever hracvdosahu (_PlayerInRange(3)) {
  SaveGameValue(62, 0);
  SetAlarmType(1023, false);
  SUBTITLES_SetOn(true);
  HUMAN_Stop();
  HUMAN_SetAnim("", 400, 400, false);
  FRM_GetNearestPlayer(this, player);
  HUMAN_TurnAtNearestPlayer();
  FRM_MorphSpeechDelayed(this, 07991502, 3, 20);
  if(below_car == 1){
    HUMAN_Stop();
    HUMAN_Move("AF3a_21_below");
    HUMAN_SetMODE_Stand();
    HUMAN_Move("AF3a_21_01");
    Delay(1000);
  }
  else{
    Delay(1000);
  }
  FRM_GetNearestPlayer(this, player);
  HUMAN_TurnAtNearestPlayer();
  FRM_MorphSpeechDelayed(player, 07991503, 3, 20);
  HUMAN_TurnAtNearestPlayer();
  HUMAN_SetAnim("%%kecy3", 300, 300, true);
  FRM_GetNearestPlayer(this, player);
  HUMAN_TurnAtNearestPlayer();
  FRM_MorphSpeechDelayed(this, 07991504, 3, 20);
  HUMAN_TurnAtNearestPlayer();
  HUMAN_SetAnim("", 300, 300, false);
  HUMAN_TurnAtNearestPlayer();
  FRM_MorphSpeechDelayed(player, 07991505, 3, 20);
  HUMAN_TurnAtNearestPlayer();
  HUMAN_SetAnim("", 300, 300, false);
  Delay(300);
  SUBTITLES_SetOn(false);
  SendSignal(obj1, 4);
  SetObjectiveStatus(4, 0);
  gosub ENABLE_ALARM;
  goto ACTIVITY2;
}

Whenever AlarmsOff (_SignalReceived(19))
{
DisableAlarms(1);
goto END;
}

// -------------------------------------------------------------------------------------------------------------

gosub ENABLE_ALARM;

Label START:
  HUMAN_SETMODE_StandFast();
  HUMAN_SETMODE_Walk();
  HUMAN_SETAIMODE_Defensive();
  HUMAN_SetOptimized(1);
  HUMAN_Suspend(true);

Label ACTIVITY:
  SaveGameValue(62, 1);
  gosub RNDACTIVITY;  
  HUMAN_Move("AF3a_21_01");
  HUMAN_TurnAt(opel1);
  rnd = _RandomInt(500) + 500;
  Delay(rnd);
  HUMAN_SetAnim("%%mechanik", 300, 300, true);
  rnd = _RandomInt(5000)+5000;
  Delay(rnd);
  HUMAN_SetAnim("", 300, 300, false);
  Delay(500);
Label ACTIVITY2:
  HUMAN_Move("AF3a_21_02");
  HUMAN_TurnAt(opel2);
  Delay(200);
  gosub RNDACTIVITY;  
  rnd = _RandomInt(500) + 500;
  Delay(rnd);
  HUMAN_SetAnim("!manipul1", 300, 300, true);
  Delay(4500);
  HUMAN_SetAnim("", 300, 300, false);
  Delay(500);
goto ACTIVITY;

Label RNDACTIVITY:
  rnd = _RandomInt(10);
  Delay(200);
  if(rnd == 0)	{ HUMAN_ACTIVITY_Drinkbottle(flaska); }
  if(rnd == 1)	{ HUMAN_ACTIVITY_Hot(); }
  Delay(200);
return;

Label ENABLE_ALARM:
  SetAlarmType(1023, false);
  SetAlarmType(323, true);			// 256, 64, 2, 1
return;

Label END:
  Delay(2000);
  SUBTITLES_SetON(false);