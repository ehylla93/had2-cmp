// AF3a_18 - guard on the minaret (not optimized)
// -------------------------------------------------------------------------------------------------------------

Block{
  FRAME look_alarm;		FRM_FindFrame(look_alarm, "dummy_21_lookalarm");
  FRAME dummy_alarm;	FRM_FindFrame(dummy_alarm, "dummy_alarm");
  FRAME alarm_wp;		FRM_FindFrame(alarm_wp, "dummy_pristup01");
  FRAME look1_01;		FRM_FindFrame(look1_01, "AF3a_18_look1_01");
  FRAME look1_02;		FRM_FindFrame(look1_02, "AF3a_18_look1_02");
  FRAME look1_03;		FRM_FindFrame(look1_03, "AF3a_18_look1_03");
  FRAME look2_01;		FRM_FindFrame(look2_01, "AF3a_18_look2_01");
  FRAME look2_02;		FRM_FindFrame(look2_02, "AF3a_18_look2_02");
  FRAME look2_03;		FRM_FindFrame(look2_03, "AF3a_18_look2_03");
  FRAME look3_01;		FRM_FindFrame(look3_01, "AF3a_18_look3_01");
  INTEGER rnd;
  INTEGER firsttime	= 0;

  SetAlarmType(1023, true);					// enable all alarms
  SetAlarmType(4, false);					// disable 4: Hear steps
}

// -------------------------------------------------------------------------------------------------------------

OnDeath(){
  EndScript();
}

OnAlarm(){
  HUMAN_SetAnim("", 100, 100, false);
  HUMAN_WeaponOnArm(1);
  block{
    DisableSignals(true);
    HUMAN_Stop();
    HUMAN_SETSIGHTRANGE(450);
  }

  if(firsttime == 0){
    Delay(2000);
	SendSignal(dummy_alarm, 1);
    firsttime = 1;
  }
  HUMAN_TurnAtNearestPlayer();
  EndScript();
}

OnAlarmDone(){
  HUMAN_SetAlarm(false);
  HUMAN_SetMODE_Crouch();
  goto END;
}

OnSignal(20){
  HUMAN_SetAnim("", 100, 100, false);
  SetAlarmType(1023, false);
  HUMAN_Stop();
  HUMAN_SETMODE_Stand();
  HUMAN_SETMODE_Run();
  HUMAN_Move("AF3a_18_04");
  HUMAN_TurnAt(alarm_wp);
  HUMAN_SetAnim("!suppfireKN1a", 200, 200, 0);
  Delay(2000);
  HUMAN_Attack(look_alarm, 400);
  SetAlarmType(1023, true); 
  HUMAN_TurnAtNearestPlayer();
  EndScript();
}

Whenever AlarmsOff (_SignalReceived(19))
{
DisableAlarms(1);
}

// -------------------------------------------------------------------------------------------------------------

Label START:
  HUMAN_SETMODE_Stand();
  HUMAN_SETMODE_Guard();
  HUMAN_SETAIMODE_Defensive();
  HUMAN_SetSniper(true, 10);
  HUMAN_SetOptimized(1);

Label ACTIVITY:
  HUMAN_Move("AF3a_18_01");
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look1_01);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look1_02);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look1_03);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  gosub VYBERCINNOST;
  HUMAN_Move("AF3a_18_04");
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look2_01);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look2_02);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_TurnAt(look2_03);
  rnd = _RandomInt(5000) + 10000;  Delay(rnd);
  HUMAN_Move("AF3a_18_06");
  gosub VYBERCINNOST;
goto ACTIVITY;

Label VYBERCINNOST:
  rnd = _RandomInt(10);
  if(rnd == 0){ 
    gosub NUDA;
  }
  if(rnd == 1){ 
    gosub SMOKE;
  }
return;

Label NUDA:
  HUMAN_WeaponOnArm(0);
  Delay(200);
  HUMAN_SetAnim("%%nuda", 1000, 1000, 1);
  rnd = _RandomInt(6000) + 20000; Delay(rnd);
  HUMAN_SetAnim("", 200, 200, 0);
  Delay(300); 
  HUMAN_WeaponOnArm(1);
return;

Label SMOKE:
  HUMAN_WeaponOnArm(0);
  Delay(1000);
  HUMAN_SetAnim("%(koureniB", 1000, 1000, 0);		//(Looking Through Pockets Then Lights Fag)
  Delay(10000);
  HUMAN_SetAnim("%(koureni5", 1000, 1000, 1);		//(Brings Hand Up To Smoke)
  rnd = _RandomInt(6000) + 20000; Delay(rnd);
  HUMAN_SetAnim("", 200, 200, 0);
  Delay(300);
  HUMAN_WeaponOnArm(1);
  Return;

Label END: