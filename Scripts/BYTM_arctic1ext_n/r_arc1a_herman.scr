// Hermann

Block
{
frame sit;		FRM_FindFrame(sit,"dummy_Herman_sit");
frame see;		FRM_FindFrame(see,"tv5okno14");
integer counter;	counter = 0;
integer AIEvent;	AIEvent = 99;
frame AllKilled;	FRM_FindFrame(AllKilled, "dummy_OBJ1_sender");

SetAlarmType(1023, True);
SetAlarmType(412, False);	// 4 + 8 + 16 + 128 + 256 = False
}

HUMAN_SETAIMODE_Zombie();
HUMAN_SETMODE_Walk();
HUMAN_SETMODE_Stand();
HUMAN_Suspend(True);
goto TheEnd;

//---------------------------------------------------------------------------------------------------

OnAlarm()
{
	Block
	{
	DisableSignals(true);
	AIEvent =_GetAlarmType(); 
	PrintfNumber("Alarm Hermann: ", AIEvent);
	}
HUMAN_SetAnim("", 150, 150, False);
HUMAN_Stop();
HUMAN_SETMODE_Stand();
HUMAN_SETAIMODE_Aggressive();
EndScript();
}

OnAlarmDone()
{
SetAlarmType(1023, False);	
SetAlarmType(375, True);	
HUMAN_SetAlarm(False);
HUMAN_WeaponOnArm(True);
HUMAN_SETMODE_Walk();
HUMAN_SETAIMODE_Zombie();
HUMAN_SETMODE_Stand();
HUMAN_Move("Herman_4");
HUMAN_TurnAt(see);
goto TheEnd;
}

OnDeath()
{
Sendsignal(AllKilled, 30);
EndScript();
}

//---------------------------------------------------------------------------------------------------

Whenever A (_PlayerInRange(100))
{
HUMAN_Suspend(False);
}

Label Home:
  HUMAN_Move("Herman_4");
  HUMAN_TurnAt(see);
  Delay(1500);
  HUMAN_ACTIVITY_Sit(sit);
  goto TheEND;

OnSignal(10) // Gets out
{
  HUMAN_SETMODE_StandFast();
  HUMAN_Move("Herman_1");
  HUMAN_Move("Herman_2");
  HUMAN_Move("Herman_3");
  HUMAN_WeaponOnArm(True);
  Delay(500);
  HUMAN_SETMODE_Crouch();
  Delay(500);
  Printf("What's going on here? You have seen ghosts!");
  Delay(1500);
}

OnSignal(11) // Moves back
{
  HUMAN_SETMODE_Stand();
  HUMAN_SETMODE_Walk();
  Printf("I really hate your stupid jokes!");
  Delay(2500);
  HUMAN_WeaponOnArm(False);
  Delay(200);
  HUMAN_Move("Herman_2");
  HUMAN_Move("Herman_1");
  HUMAN_Move("Herman_4");
  Delay(500);
  HUMAN_TurnAt(see);
  Delay(350);
  HUMAN_ACTIVITY_Sit(sit);
}

OnSignal(12) 
{
  HUMAN_SETMODE_Stand();
  HUMAN_SETMODE_Walk();
  HUMAN_WeaponOnArm(False);
  HUMAN_Move("Herman_3");
  HUMAN_SETSIGHTRANGE(50); 
  HUMAN_SETHEARRANGE(60);

  label loop:
    counter = counter + 1;
    If (counter > 3 ) 
      {
          counter = 0;
          Delay(1500);
          goto home;
       }
    delay(500);
    HUMAN_SetAnim("%%turnleft", 450, 500, False);                     
    delay(2300);
    HUMAN_SetAnim("%%turnright", 450, 500, False);
    delay(2300);
  goto loop;
}

Whenever AlarmsOff (_LoadGameValue(16)==1)
{
DisableAlarms(1);
}

label TheEND: