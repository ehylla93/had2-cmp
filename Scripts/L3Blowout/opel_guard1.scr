Integer  suspended             = 1;
// Nasledujici hodnoty se v initu naplni smysluplnou hodnotou
Integer  unsuspend_range  = 10; 	 // V Initu naplnime smysluplnou hodnotou
Integer  alarm_sight_range = 180;            //  Na jakou vzdalenost vidi pri alarmu


// -------------------------------------------------------------------------------------------------------------
// Alarm and AI event settings
// 1   - blizka strelba    2   - strelba     4     - kroky                    8     - zvuky (OnUse)    16   - exploze
// 32 - vidim                 64 - zraneni    128 - smrtelny vykrik    256 - friendly fire         512 - vidim mrtvolu
// -------------------------------------------------------------------------------------------------------------
SetAlarmType(512, false);
SetAlarmType(256, false);

// ----------------------------------------------------------------------------------------------------
// AKTIVACE ALARMU
// ----------------------------------------------------------------------------------------------------
OnAlarm()
{
      Block { DisableSignals(1);  DisableWhenevers(1); }
      HUMAN_SETSIGHTRANGE(180);
      EndScript();
      GoTo End;
}


// ----------------------------------------------------------------------------------------------------
// KONEC ALARMU
// ----------------------------------------------------------------------------------------------------
OnAlarmDone()
{
      SetAlarmType(512, false);
      SetAlarmType(256, false);
      Block { DisableSignals(0);  DisableWhenevers(0); }
      GoTo GuardLoop;
}


// ----------------------------------------------------------------------------------------------------
// SMRT
// ----------------------------------------------------------------------------------------------------
OnDeath()
{
      EndScript( );
}


// ----------------------------------------------------------------------------------------------------
// SIGNALY
// ----------------------------------------------------------------------------------------------------
EnableSignal(1, true);
OnSignal(1)	// received from OPEL_GUNNER OnAlarm()
{
      if (suspended == 1)
      {
          printf("!!!!! Opel-Guard1 - CHYBA - SUSPENDOVANY PANAK !!!!!");
      } 
     // Zde patri nejaky obsluzny kod
     HUMAN_SETMODE_Crouch();
     EnableSignal(1, false);
     GoTo GuardLoop;
}


DisableWhenevers( 1 );
// ----------------------------------------------------------------------------------------------------
// UNSUSPENDOVANI
// ----------------------------------------------------------------------------------------------------
Whenever "player_in_range" ( (suspended == 1) and _PlayerInRange(unsuspend_range) )
{
       Block { DisableSignals(1);  DisableWhenevers(1); }
       HUMAN_Suspend(false);
       HUMAN_SetOptimized(2);
       suspended=0;   
       Block { DisableSignals(0);  DisableWhenevers(0); }
       GoTo OnAction;
}


// ----------------------------------------------------------------------------------------------------
// INIT
// ----------------------------------------------------------------------------------------------------
Delay(300);  // Pro jistotu, at se stihnou nastavit GameValue
Block
{
    unsuspend_range  = _LoadGameValue( 1 );
    alarm_sight_range = _LoadGameValue( 2 );
}

SetWheneverTime("player_in_range", 2000);
HUMAN_SETAIMODE_Defensive();
HUMAN_Suspend(true);
DisableWhenevers(0);

goto End;

Label OnAction:

Label GuardLoop:
HUMAN_SETMODE_Guard();
HUMAN_LookAround(5);
HUMAN_SetOptimized( 2 );
HUMAN_Move("opel_guard_1_1");
HUMAN_Move("opel_guard_1_2");
goto GuardLoop;

// ----------------------------------------------------------------------------------------------------
Label End:
