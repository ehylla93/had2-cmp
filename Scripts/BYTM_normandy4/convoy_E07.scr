// convoy_E07 on patrol

block
{
SetAlarmType(128,0);

Frame talk;		FRM_FindFrame(talk, "talk_convoy_E07");
Frame reinf4;	FRM_FindFrame(reinf4, "reinf4");
integer aievent;

Frame MyFRM;	FRM_GetMyFrame(MyFRM);

Integer a = _RandomInt(4);
Integer c=0;
Integer time;
}

if(a==0) { HUMAN_SETAIMODE_Zombie(); }
if(a==1) { HUMAN_SETAIMODE_Aggressive(); }
else { HUMAN_SETAIMODE_Defensive(); }

FRM_TeleportNearCheckpoint(MyFRM, "way1_06", "");
HUMAN_SETMODE_Guard();
goto rdmroute;

//------------------------------------------------------------------------------------------

OnAlarm()
{
SendSignal(talk, 2);
HUMAN_SetAnim("", 200, 200, 0);
block
{
DisableSignals(1);
aievent=(_GetAlarmType());
}
PrintfNumber("convoy_E07 alarm:",aievent);
EndScript();
}

OnAlarmDone()
{
block
{
HUMAN_SetAlarm(0);
SetAlarmType(1023,1);
SetAlarmType(648,0);
}
HUMAN_SETMODE_Crouch();
goto end;
}

OnDeath()
{
printf("convoy_E07 dead");
SendSignal(talk, 2);
SendSignal(reinf4, 1);
EndScript();
}

Label rdmroute:
a=_RandomInt(3);
if(a==0) { printf("convoy_E07 taking route1"); goto route1; }
if(a==1) { printf("convoy_E07 taking route2"); goto route2; }
if(a==2) { printf("convoy_E07 taking route3"); goto route3; }

Label route1:
HUMAN_Move("way1_02");
gosub timer;
HUMAN_Move("way1_03");
HUMAN_Move("way1_04");
gosub timer;
HUMAN_Move("way1_03");
gosub timer;
HUMAN_Move("way1_02");
HUMAN_Move("way1_06");
goto rdmroute;

Label route2:
a=_RandomInt(2);
if(a==0) { HUMAN_Move("way1_08"); }
HUMAN_Move("way1_05");
gosub timer;
a=_RandomInt(2);
if(a==0) { HUMAN_Move("way1_08"); }
HUMAN_Move("way1_06");
goto rdmroute;

Label route3:
HUMAN_Move("way1_07");
gosub timer;
goto rdmroute;

Label timer:
time=((_RandomInt(20000)) + 5000);	// min 5 seconds, max 25 seconds
printfnumber("convoy_E07 timer", time);
Delay(time);
c=c+1;
gosub rdmanim;
if(c==2) { c=0; Return; }
goto timer;

Label rdmanim:
a=_RandomInt(4);
if(a==0)
{
HUMAN_WeaponOnArm(0);
Delay(1000);
HUMAN_SetAnim("%%neslysim", 500, 500, 0); 
Delay(4000);
HUMAN_WeaponOnArm(1);
}
if(a==1)
{
HUMAN_WeaponOnArm(0);
Delay(1000);
HUMAN_SetAnim("%%divase", 500, 500, 0); 
Delay(6000);
HUMAN_WeaponOnArm(0);
}
if(a==2)
{
HUMAN_WeaponOnArm(0);
Delay(1000);
HUMAN_SetAnim("%%protahovani", 500, 500, 0); 
Delay(7000);
HUMAN_WeaponOnArm(1);
}
if(a==3)
{
HUMAN_WeaponOnArm(0);
Delay(1000);
HUMAN_SetAnim("%%flus1", 500, 500, 0); 
Delay(4000);
HUMAN_WeaponOnArm(1);
}

Delay(1000);
Return;

Whenever AlarmsOff (_LoadGameValue(16)==1)
{
DisableAlarms(1);
FRM_TeleportNearCheckpoint(MyFRM, "way1_06", "");
HUMAN_SETMODE_Guard();
SendSignal(talk, 1);	//whistling
goto rdmroute;
}

//------------------------------------------------------------------------------------------

label end: