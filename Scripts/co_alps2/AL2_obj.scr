// AL2_obj - objectives
// -------------------------------------------------------------------------------------------------------------
// Variables definition
block
{
Frame door;			FRM_FindFrame(door, "B03_k_19");
Frame agent;		FRM_FindFrame(agent, "AL2_Agent");

integer objdoc=0;

SetWhenever(obj5b, false);
}

goto end;

// -------------------------------------------------------------------------------------------------------------
// Whenever keywords and signals

Whenever obj1(_SignalReceived(1))		//Player did not manage to hand documents to officer - TIME OUT
{
	DisableSignals(1);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993041);
	Delay(3000);
	SetObjectiveStatus(1, 3);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj1kill(_SignalReceived(25))		//Player kills before completing warehouse objective (Officer killed)
{
	SetObjectiveStatus(3, 0);
	SetObjectiveStatus(1, 2);
	SetObjectiveStatus(2, 2);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993096);		//"OBJECTIVE FAILED: OFFICER KILLED"
	Delay(3000);
	SUBTITLES_SetText(15993002);		//"NEW OBJECTIVE: CONTACT AGENT SALTER, SHE'LL TAKE YOU TO THE ARCHIVES."
	Delay(3000);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj1killsoldier(_SignalReceived(26))		//Player kills before completing warehouse objective (Soldier killed)
{
	SetObjectiveStatus(3, 0);
	SetObjectiveStatus(1, 2);
	SetObjectiveStatus(2, 2);
	SUBTITLES_SetText(11992212);		//"OBJECTIVE FAILED"
	Delay(3000);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993002);		//"NEW OBJECTIVE: CONTACT AGENT SALTER, SHE'LL TAKE YOU TO THE ARCHIVES."
	Delay(3000);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj2(_SignalReceived(2))		//Player is inside store
{
	SetObjectiveStatus(3, 0);
	SetObjectiveStatus(2, 1);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993044);		//"OBJECTIVE COMPLETED: SOLDIERS DISPATCHED."
	Delay (4000);
	SUBTITLES_SetText(15993002);		//"NEW OBJECTIVE: CONTACT AGENT SALTER, SHE'LL TAKE YOU TO THE ARCHIVES."
	Delay(4000);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj2no(_SignalReceived(24))		//Player is NOT inside store
{
	DisableSignals(1);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993087);		//"OBJECTIVE FAILED: SOLDIERS FROM THE WAREHOUSE RAISED THE ALARM."
	Delay (4000);
	SetObjectiveStatus(2, 3);
	goto end;
}

Whenever obj2b(_SignalReceived(3))		//Player was spotted by an officer (AL2_alarm)
{
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(11992211);		//"OBJECTIVE FAILED: ALARM RAISED."
	Delay (4000);
	SetObjectiveStatus(4, 3);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj3(_SignalReceived(4))		//Player contacted Agent Salter
{
	SetObjectiveStatus(4, 0);
	SetObjectiveStatus(3, 1);
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993045);		//"OBJECTIVE COMPLETED: AGENT SALTER FOUND."
	Delay (4000);
	SUBTITLES_SetText(15993046);		//"NEW OBJECTIVE: GO WITH AGENT SALTER TO GET THE DOCUMENTS."
	Delay (4000);
	SUBTITLES_SetText(15993049);		//"NEW OBJECTIVE: AGENT SALTER MUST NOT BE HARMED."
	SetObjectiveStatus(7, 0);
	Delay (4000);
	SUBTITLES_SetOn(0);
	GoTo end;
}

Whenever obj4(_SignalReceived(5))		//Player has the documents
{
	objdoc=1;
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993047);		//"OBJECTIVE COMPLETED: STOLEN THE DOCUMENTS."
	Delay (4000);
	SUBTITLES_SetText(15993004);		//"NEW OBJECTIVE: RUN TO THE GATE."
	SetObjectiveStatus(5, 0);
	SetObjectiveStatus(4, 1);
	Delay (4000);
	SUBTITLES_SetOn(0);
	SetWhenever(obj5b, true);		//Obj5 activated (Gather at the gate)
	GoTo end;
}

Whenever obj1ok(_SignalReceived(7))		//Player handed out documents
{
	SetObjectiveStatus(2, 0);
	SetObjectiveStatus(1, 1);
	goto end;
}

Whenever obj7(_SignalReceived(8))		// Agent Salter dead
{
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993050);		//"OBJECTIVE FAILED: AGENT SALTER KILLED."
	SetObjectiveStatus(7, 3);
	Delay (4000);
	SUBTITLES_SetOn(0);
	GOTO END;
}

Whenever obj5b((_MP_AllTeamMembersAreInRange(1, 5)) AND (_FrameInRange(agent) < 5))
{
	DisableSignals(1);
	DisableWhenevers(1);
	if(objdoc==0)
	{
	SendSignal(agent, 4);
	}
	SUBTITLES_SetOn(1);
	SUBTITLES_SetText(15993048);		//"OBJECTIVE COMPLETED: ESCAPED FROM THE CASTLE."
	Delay(3000);
	SUBTITLES_SetOn(0);
	SetObjectiveStatus(5, 1);
	SetObjectiveStatus(7, 1);
	GoTo end;
}

OnSignal(11)
{
objdoc=0;
goto end;
}

OnSignal(12)
{
objdoc=1;
goto end;
}

// -------------------------------------------------------------------------------------------------------------

Whenever objtext (_SignalReceived(9))
{
SetWhenever(objtext, false);
SUBTITLES_SetOn(1);
SUBTITLES_SetText(15993005);	//"NEW OBJECTIVE: TAKE COVER SO THE OFFICERS DON'T SEE YOU."
Delay(3500);
SUBTITLES_SetText(15993001);	//"NEW OBJECTIVE: GIVE THE PAPERS TO THE OFFICER."
Delay(3500);
SetObjectiveStatus(1, 0);		//"Hand out the documents"
SUBTITLES_SetOn(0);
}

Label end: