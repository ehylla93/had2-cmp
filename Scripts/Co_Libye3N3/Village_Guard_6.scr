Block
{
     Integer  alarm_sight_range = 180;         //  Na jakou vzdalenost vidi pri alarmu
     Integer unsuspend_range = 10;
     Integer is_enabled = 1;
     integer is_signaled = 0;
     // -------------------------------------------------------------------------------------------------------------
     // Alarm and AI event settings
     // 1   - blizka strelba    2   - strelba     4     - kroky                    8     - zvuky (OnUse)    16   - exploze
     // 32 - vidim                 64 - zraneni    128 - smrtelny vykrik    256 - friendly fire         512 - vidim mrtvolu
     // -------------------------------------------------------------------------------------------------------------
    SetAlarmType(1023, false);
}

// ----------------------------------------------------------------------------------------------------
// AKTIVACE ALARMU
// ----------------------------------------------------------------------------------------------------
OnAlarm()
{
      Block { DisableSignals(1);  DisableWhenevers(1); }
      HUMAN_SETSIGHTRANGE( alarm_sight_range );
      HUMAN_SETAIMODE_Zombie();
      EndScript();
}

// ----------------------------------------------------------------------------------------------------
// KONEC ALARMU
// ----------------------------------------------------------------------------------------------------
OnAlarmDone()
{
   block {
      SetAlarmType(1023, false);
      SetAlarmType(1, true);
      SetAlarmType(4, true);
      SetAlarmType(16, true);
      SetAlarmType(32, true);
      SetAlarmType(64, true);
      SetAlarmType(128, true);
   }
   Block { DisableSignals(0);  DisableWhenevers(0); }
   EnableSignal(1, true);
   GoTo End;
}

// ----------------------------------------------------------------------------------------------------
// KONEC ALARMU
// ----------------------------------------------------------------------------------------------------
OnDeath()
{
      EndScript();
}

// ----------------------------------------------------------------------------------------------------
// SIGNALY
// ----------------------------------------------------------------------------------------------------
EnableSignal(1, true);
OnSignal(1)		// received from TIGER_DETECTOR
{
   printf("GUARD SIGNALED!");
   block {
      EnableSignal(1, false);
      HUMAN_Suspend(false);
      SetWhenever("player_in_range", false);
      SetAlarmType(1023, true);
      SetAlarmType(512, false);
      SetAlarmType(256, false);
      is_signaled = 1;
   }
   HUMAN_WeaponOnArm(true);
   HUMAN_SETMODE_Stand();
   HUMAN_SETMODE_Run();
   HUMAN_SETSIGHTRANGE(alarm_sight_range);
   HUMAN_Move("villtarget_1_6");
   HUMAN_LookAround(4);
   goto End;
}

// ----------------------------------------------------------------------------------------------------
// UNSUSPENDOVANI
// ----------------------------------------------------------------------------------------------------
Whenever "player_in_range" (_PlayerInRange(unsuspend_range) )
{
   block {
      SetAlarmType(1023, false);
      SetAlarmType(1, true);
      SetAlarmType(4, true);
      SetAlarmType(16, true);
      SetAlarmType(32, true);
      SetAlarmType(64, true);
      SetAlarmType(128, true);
   }
   HUMAN_Suspend( false );
   GoTo End;	
} 

  // ----------------------------------------------------------------------------------------------------
   // INIT
   // ----------------------------------------------------------------------------------------------------
   HUMAN_Suspend(true);  
   Delay(300);  // Pro jistotu, at se stihnou nastavit GameValue   
   Block
   {
      alarm_sight_range = _LoadGameValue( 2 );
      SetWheneverTime("player_in_range", 3000);
   }
   GoTo  End;

// ----------------------------------------------------------------------------------------------------
Label End:


