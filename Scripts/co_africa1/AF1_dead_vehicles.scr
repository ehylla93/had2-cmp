// AF1_dvere
// -------------------------------------------------------------------------------------------------------------

block{
  FRAME opel;			FRM_FindFrame(opel, "la_OpelFuj");
  FRAME jeep;			FRM_FindFrame(jeep, "la_Jeepsas_01");
  INTEGER a_opel;
  INTEGER a_jeep;
}

goto END;

// -------------------------------------------------------------------------------------------------------------

// Checks if last objective active
Whenever l_obj_active (_LoadGameValue(18))
{
  a_opel = _ACTOR_GetState(opel);
  a_jeep = _ACTOR_GetState(jeep);
  SUBTITLES_SetOn(true);
  if((a_opel==1) AND (a_jeep==1))
  {
    SUBTITLES_SetText(05991071);	//"NEW OBJECTIVE - GATHER NEAR THE SAS JEEP OR THE FLAK VEHICLE"
    SetObjectiveStatus(9, 0);
  }
  else
  {
    if(a_opel==1)
    {
      SUBTITLES_SetText(05011016);		//"NEW OBJECTIVE: ASSEMBLE THE SQUAD ON THE TRUCK WITH THE FLAK CANNON."
      SetObjectiveStatus(6, 0);
    }
    if(a_jeep==1)
    {
      SUBTITLES_SetText(05991070);		//"NEW OBJECTIVE - GATHER NEAR THE SAS JEEP"
      SetObjectiveStatus(8, 0);
    }
  }
  SetObjectiveStatus(1, 4);
  SetObjectiveStatus(2, 1);
  SetObjectiveStatus(3, 1);
  Delay(3500);
  SUBTITLES_SetOn(false);
  goto END;
}

Whenever allwdead ((_ACTOR_GetState(opel) == 0) AND (_ACTOR_GetState(jeep) == 0))
{
  goto LAST_OBJ_FAILED;
}

Whenever opeldead ((_ACTOR_GetState(opel)==0) AND (_LoadGameValue(18)))
{
  SetWhenever(opelznicen, false);
}

Whenever jeepdead ((_ACTOR_GetState(jeep)==0) AND (_LoadGameValue(18)))
{
  SetWhenever(jeepznicen, false);
}

Whenever opelznicen ((_ACTOR_GetState(opel)==0) AND (_LoadGameValue(18)))
{
  a_jeep = _ACTOR_GetState(jeep);
  if(a_jeep == 1)
  {
    SUBTITLES_SetOn(true);
    SUBTITLES_SetText(05991072);	//"OBJECTIVE CHANGED - GATHER NEAR THE SAS JEEP"
    block
    {
      SetObjectiveStatus(9, 4);
      SetObjectiveStatus(6, 4);
      SetObjectiveStatus(8, 0);
    }
    Delay(3500);
    SUBTITLES_SetOn(false);
  }
  else
  {
    goto LAST_OBJ_FAILED;   
  }
}

Whenever jeepznicen ((_ACTOR_GetState(jeep)==0) AND (_LoadGameValue(18)))
{
  a_opel = _ACTOR_GetState(opel);
  if(a_opel == 1)
  {
    SUBTITLES_SetOn(true);
    SUBTITLES_SetText(05991073);	//"OBJECTIVE CHANGED - GATHER NEAR THE FLAK VEHICLE"
    block
    {
      SetObjectiveStatus(9, 4);
      SetObjectiveStatus(8, 4);
      SetObjectiveStatus(6, 0);
    }
    Delay(3500);
    SUBTITLES_SetOn(false);
  }
  else
  {
    goto LAST_OBJ_FAILED;   
  }
}

// -------------------------------------------------------------------------------------------------------------

Label LAST_OBJ_FAILED:
    SUBTITLES_SetOn(true);
    SUBTITLES_SetText(05011003);	//"OBJECTIVE FAILED: VEHICLE REQUIRED TO LEAVE THE AIRPORT WAS DESTROYED."
    Delay(3500);
    SUBTITLES_SetOn(false);    
  block
  {
    SetObjectiveStatus(6 ,4);
    SetObjectiveStatus(8 ,4);
    SetObjectiveStatus(9, 3);
  }

Label END: