// AF1_11 - Officier near main building, patrols and smokes
// -------------------------------------------------------------------------------------------------------------

block{
  FRAME dummyalert;		FRM_FindFrame(dummyalert, "dummy_alert");
  FRAME look;			FRM_FindFrame(look, "af1_dummy10");
  FRAME look1;			FRM_FindFrame(look1, "m_palma5");
  FRAME look2;			FRM_FindFrame(look2, "af1_dummy11");
  FRAME sit;			FRM_FindFrame(sit, "af1_dummy22");
  FRAME af12;			FRM_FindFrame(af12, "af1_12");
  FRAME dummy_point;	FRM_FindFrame(dummy_point, "Cylinder03");
  INTEGER atpos			= 0;	// if 1, soldier is hiding in the main building.

  SetAlarmType(1023, true);
  SetAlarmType(768, false);
}

  HUMAN_SETMODE_Guard();
  HUMAN_SETAIMODE_Defensive();
  HUMAN_SETEVENTS(true); 
goto ACTIVATE;

// -------------------------------------------------------------------------------------------------------------

OnSignal(20){
  DisableSignals(true);
  SetAlarmType(256, false);
  goto ALERT;
}

OnDeath(){
  EndScript();
}

OnAlarm(){
  block{
    HUMAN_Stop();
    DisableSignals(true);
    HUMAN_SETMODE_Stand();
    HUMAN_SETSIGHTRANGE(200);
    HUMAN_SETMODE_Run();
  }
  HUMAN_SETMODE_Crouch();
  SendSignal(dummyalert, 1);

Label ALERT:
  if(!atpos){
    HUMAN_SetMode_Run();
    HUMAN_Move("af1_11_ad");
    atpos = 1;
    HUMAN_TurnAtNearestPlayer();
    HUMAN_WeaponOnArm(true);
    block
    {
      HUMAN_SetAlarm(false);
      SetAlarmType(1023, true);
      SetAlarmType(512, false);
    }
    goto END;
  }
  HUMAN_WeaponOnArm(true);
  EndScript();
}

OnAlarmDone(){
  SetAlarmType(1023, true);
  SetAlarmType(512, false);
  HUMAN_SETSIGHTRANGE(100);
  HUMAN_SetAlarm(false);
  if(atpos){
    goto END;
  }
  else{
    goto START;
  }
}

Whenever AlarmsOff (_SignalReceived(19))
{
DisableAlarms(1);
goto END;
}

// -------------------------------------------------------------------------------------------------------------

Label ACTIVATE:
  HUMAN_SetOptimized(true);

Label START:
  HUMAN_Move("af1_11_01");
  HUMAN_TurnAt(look);
  gosub SMOKE;
  HUMAN_Move("af1_11_02");
  HUMAN_TurnAt(look1);
  HUMAN_Move("af1_11_03");
  HUMAN_TurnAt(look2);
  Delay(1500);
  HUMAN_ACTIVITY_Sit(sit);
  HUMAN_SetAnim("%%sedim2", 1000, 1000, 0);
  Delay(15000);
  HUMAN_SetAnim("", 1000, 1000, 0);
  
Delay(1000);
HUMAN_SETMODE_Stand();
HUMAN_Move("af1_11_04");
Delay(1000);
HUMAN_Move("af1_11_03");
HUMAN_TurnAt(look2);
HUMAN_Move("af1_11_02");
HUMAN_TurnAt(look1);
goto START;

Label SMOKE:
Delay(2000);
HUMAN_SetAnim("%(koureniB", 1000, 1000, 0);		//(Looking Through Pockets Then Lights Fag)
Delay(10000);
	Label SMOKE2:
	HUMAN_WeaponOnArm(0);
	HUMAN_SetAnim("%(koureni5", 1000, 1000, 0);		//(Brings Hand Up To Smoke)
	Delay(10000);
	HUMAN_SetAnim("%(koureni5", 1000, 1000, 0);		//(Brings Hand Up To Smoke)
	Delay(10000);
	HUMAN_SetAnim("%(koureni5", 1000, 1000, 0);		//(Brings Hand Up To Smoke)
	Delay(10000);
return;

Label END: