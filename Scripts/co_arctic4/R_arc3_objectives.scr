// objectives info and chenging for mission Arctic3
// !!! HODIT NA DUMAC K VYSILACCE

// TED *    DRIV
//   1    *     1     "Proved pruzkum"
//   3    *     2     "Locate Radio"
//   6    *     3     "Use the radio - inform what's going on"
//   5    *     4     "Occupy the u-boat."
//   4    *     5     "Prevent from submerging"
//   2    *     6     "Documents"
//   7    *     7     "All tema members must survive" (deactivated)
//   8    *     8     "Stealth! (optional)"

Block
{
  SetObjectiveStatus(1, 0);	// Explore Iceberg
  SetObjectiveStatus(2, 0);	// Documents
  
  integer counter1 = 0;
  integer counter2 = 0;
  integer O2B4J = 0;
  integer What = 0; 
  integer OBJ_Doc = 0;
  integer break_signal_7 = 0;
  integer uvodni_kecy=0;
  
  Frame MyFRM;		FRM_GetMyFrame(MyFRM);
  Frame dabing;		FRM_Findframe(dabing, "dummy_dabing_radio");
  Frame Radio;		FRM_Findframe(Radio, "m_AF5_prisj_2");
  frame zvuk;		FRM_FindFrame(zvuk, "S_radio");

  SetWhenever(OBJ_3, False);

  SaveGameValue(2,0);
  SaveGameValue(4,0);
  SaveGameValue(5,0);
}


// Explore Iceberg - 1
Whenever OBJ_1 (_PlayerInRange(82))			
  {
    SetObjectiveStatus(3, 0);	//Locate radio
    SetActorState(Radio, 1);
    SetObjectiveStatus(4, 0);	//Prevent submerging
    SetObjectiveStatus(1, 1);
    PlayMusic(36);
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990810); //"OBJECTIVE ACCOMPLISHED: ICEBERG EXPLORED, GERMAN BASE LOCATED."
    Delay(3500);
    SUBTITLES_SetText(04990802); //"NEW OBJECTIVE: LOCATE THE RADIO STATION."
    Delay(3500);
    SUBTITLES_SetText(04990805); //"NEW OBJECTIVE: PREVENT THE SUBMARINE FROM SUBMERGING."
    Delay(3500);
    SUBTITLES_SetOn(False);
  }

// Locate radio - 2
Whenever OBJ_2 ( _ACTOR_GetState(Radio) == 2 )
  {
    SetObjectiveStatus(6, 0);	// Send message
    SetObjectiveStatus(3, 1);
    FRM_SetOn(zvuk, false);
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990811);	//"OBJECTIVE ACCOMPLISHED: RADIO STATION LOCATED."
    Delay(3000);
	SendSignal(dabing, 3);
	Delay(15000);
    If (O2B4J == 0) {O2B4J = 2}
    If (O2B4J == 4)
      {
         SUBTITLES_SetText(04990813);		//"OBJECTIVE ACCOMPLISHED: SUBMARINE SEIZED"
         SetObjectiveStatus(5, 1);
         SaveGameValue(5,99);
         Delay(4000);
         SetActorState(Radio, 1);
         SetWhenever( OBJ_3 , true);    
       } else {
         SetObjectiveStatus(5, 0);
         SUBTITLES_SetText(04990804);		//"NEW OBJECTIVE: SEIZE THE SURFACED SUBMARINE."
         Delay(4000);
       }
    SUBTITLES_SetText(04990803);			//"NEW OBJECTIVE: INFORM COMMAND BY RADIO OF THE SUCCESSFUL COMPLETION OF THE MISSION."
    Delay(4000);
    SUBTITLES_SetOn(False);
    goto TheEnd;
  }

// Prevent from submerging - 5
Whenever HOLD ( _SignalReceived(12))
  {
     Block
     {
        SetObjectiveStatus(4, 1);		//""
        SaveGameValue(4,99);
        break_signal_7 = 1;
        SUBTITLES_SetOn(True);
      }
     SUBTITLES_SetText(04990814);		//"OBJECTIVE ACCOMPLISHED: SUBMARINE PREVENTED FROM SUBMERGING"
     Delay(4000);
     SUBTITLES_SetOn(False);
  }

// Send "Mission accomplished" - 3
Whenever OBJ_3 ( _ACTOR_GetState(Radio) == 2 )
  {
    If ((OBJ_doc == 1))
       {
          Block
            {
			DisableSignals(True);
			DisableWhenevers(True);
			SUBTITLES_SetOn(True);
			SendSignal(dabing, 1);     
            }
          Delay(17000);
          SUBTITLES_SetText(04990812);		//"OBJECTIVE ACCOMPLISHED: MESSAGE TO COMMAND SENT"
          Delay(4000);
          SUBTITLES_SetOn(False);
		  SetObjectiveStatus(6, 1);
          goto TheEnd;
       } else {
          Delay(2500);
          SetWhenever( OBJ_3 , true); 
          SetActorState(Radio, 1);
       }
  }

// Seize u-boat (kill all marines) - 4
OnSignal(10)
  {
     If (O2B4J == 0) { O2B4J = 4}
     If (O2B4J == 2) 
      { 
         SetObjectiveStatus(5, 1);
         SaveGameValue(5,99);
         SUBTITLES_SetOn(True); 
         SUBTITLES_SetText(04990813);		//"OBJECTIVE ACCOMPLISHED: SUBMARINE SEIZED"
         Delay(4000);
         SetActorState(Radio, 1);
         SetWhenever( OBJ_3 , true);
         SUBTITLES_SetOn(False);
      } else {
      }
      SendSignal(MyFRM, 12);
  }
OnSignal(7)
  {
    If ( break_signal_7 == 1 ) { goto TheEnd; }
    SetObjectiveStatus(4, 3);
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990824);		//"OBJECTIVE FAILED: SUBMARINE SUBMERGED AND ESCAPED."
    Delay(4000);
    SUBTITLES_SetOn(False);
  }

//  Documents
OnSignal(24)
  {
   Block
    {
    SetObjectiveStatus(2, 1);
    PlayMusic(36);
    SaveGameValue(2,99);
    OBJ_Doc = 1;
    }
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990815);		//"OBJECTIVE ACCOMPLISHED: DOCUMENTS FOUND."
    Delay(4000);
    SUBTITLES_SetOn(False);
  }
OnSignal(25)
  {
   Block
    {
    SetObjectiveStatus(2, 0);
    SaveGameValue(2,0);
    OBJ_Doc = 0;
    }
    If  (_SPGetGameType()==2) { goto TheEnd;}
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990880);		//"DOCUMENTS LOST - YOU HAVE TO FIND THEM NOW!"
    Delay(4000);
    SUBTITLES_SetOn(False);
  }
OnSignal(26)
  {
   Block
    {
    SetObjectiveStatus(2, 1);
    SaveGameValue(2,99);
    OBJ_Doc = 1;
    }
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990826);		//"DOCUMENTS RECOVERED."
    Delay(4000);
    SUBTITLES_SetOn(False);
  }
OnSignal(30)
{
    SetObjectiveStatus(2, 3);
    SUBTITLES_SetOn(True);
    SUBTITLES_SetText(04990881);		//"DOCUMENTS LOST - NO WAY TO FIND THEM AGAIN!"
    Delay(4000);
    SUBTITLES_SetOn(False);  
}

Label TheEnd:
    If (uvodni_kecy !=0 ) { SUBTITLES_SetOn(False); }
    uvodni_kecy = uvodni_kecy + 1; 