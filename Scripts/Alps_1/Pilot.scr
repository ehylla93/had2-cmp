// Allied Pilot
// ---------------------------------------------

Block
{
	Frame Obj2;			FRM_FindFrame(Obj2, "obj2");
	Frame Obj3;			FRM_FindFrame(Obj3, "obj3");
	Frame AllyJeep;		FRM_FindFrame(AllyJeep, "Jeep");
	Frame me;			FRM_GetMyFrame(me);
	Frame Player;

	FRM_SwitchFaceTexture(me,"e_f058");
	HUMAN_SETSIGHTRANGE(0);
	HUMAN_SETHEARRANGE(0);
	SetAlarmType(1023, 0);

	Integer Odchazime;
			Odchazime = 0;
		
	Integer AIEvent;
	Integer SayStyle;

	HUMAN_SetOptimized(1);
	HUMAN_Suspend(1);
}
goto END;

// ---------------------------------------------

OnDeath(){
goto END;
}

OnAlarm(){
	AIEvent = _GetAlarmType();
	SayStyle = _RandomInt(2);

	If(AIEvent == 32)
	{
		HUMAN_SETMODE_Lie();
		
		If(SayStyle == 0)
		{
			FRM_MorphSpeech(me, 90004304, 10, 50);
		}

		If(SayStyle == 1)
		{
			FRM_MorphSpeechDelayed(me, 90000974, 10, 50);
			Delay(200);
			FRM_MorphSpeech(me, 90000910, 10, 50);
		}
	}

	If((AIEvent == 1) OR (AIEvent == 2))
	{
		HUMAN_SETMODE_Lie();
		HUMAN_SETMODE_Run();
		HUMAN_MoveFromFrame(Player, 6);

		If(SayStyle == 0)
		{
			FRM_MorphSpeech(me, 90000954, 10, 50);
		}

		If(SayStyle == 1)
		{
			FRM_MorphSpeech(me, 90000901, 10, 50);
		}

	}

	If(AIEvent == 64)
	{
		Delay(200);
		HUMAN_SETMODE_Lie();
		HUMAN_SETMODE_Run();
		FRM_GetNearestPlayer(me, Player);

		If(SayStyle == 0)
		{
			FRM_MorphSpeech(me, 90000805, 10, 50);
		}

		If(SayStyle == 1)
		{
			FRM_MorphSpeech(me, 90000936, 10, 50);
		}

		HUMAN_MoveFromFrame(Player, 8);
	}

	If(AIEvent == 4)
	{
		HUMAN_SETMODE_Lie();

		If(SayStyle == 0)
		{
			FRM_MorphSpeech(me, 90000947, 10, 50);
		}

		If(SayStyle == 1)
		{
			FRM_MorphSpeech(me, 90000913, 10, 50);
		}

	}

	Delay(12000);
	HUMAN_SetAlarm(0);
	
	If(SayStyle == 0)
	{
		FRM_MorphSpeechDelayed(me, 90004201, 10, 50);
		Delay(100);
		FRM_MorphSpeech(me, 90000790, 10, 50);
	}

	If(SayStyle == 1)
	{
		FRM_MorphSpeechDelayed(me, 90000982, 10, 50);
		Delay(100);
		FRM_MorphSpeech(me, 90000772, 10, 50);
	}

	HUMAN_SETMODE_Crouch();
	goto Follow;
}

Whenever Player (_PlayerInRange(50))
{
	HUMAN_Suspend(0);
	HUMAN_SETMODE_Crouch();
	HUMAN_SetAnim("%%SedimL4", 4000, 4000, 1);
	goto END;
}

Whenever Player_Rescuing (_PlayerInRange(2))
{
	Delay(500);
	FRM_MorphSpeechDelayed(me, 03990611, 10, 50);
	HUMAN_SetAnim("", 1000, 1000, 1);
	Delay(250);
	FRM_MorphSpeech(me, 03990612, 10, 50);
	Delay(1000);
	HUMAN_SetAnim("%%kecy1", 2000, 2000, 0);
	HUMAN_SETMODE_Stand();
	HUMAN_TurnAtNearestPlayer();
	Delay(6500);
	HUMAN_SetAnim("", 0, 0, 1);
	Delay(500);
	SendSignal(Obj2, 1);
	Odchazime = 1;
	HUMAN_WeaponOnArm(1);
	Delay(500);
	FRM_MorphSpeech(me, 01990290, 10, 50);
	HUMAN_SETSIGHTRANGE(80);
	HUMAN_SETHEARRANGE(60);
	SetAlarmType(103, 1);
	goto Follow;
}

Whenever Boarding_Jeep (_FrameInRange(AllyJeep) < 15)
{
	HUMAN_SETMODE_Stand();
	HUMAN_SETMODE_Walk();
	HUMAN_MoveToFrame(AllyJeep, 2);
	HUMAN_BoardVehicle("Jeep", 1, 3);
	SendSignal(Obj3, 1);
	goto END;
}

Label Follow:

If(Odchazime == 1)
{
	If(_PlayerInRange(40))
	{
		FRM_GetNearestPlayer(me, Player);
		HUMAN_MoveToFrame(Player, 0);

		If(_PlayerInRange(2))
		{
			HUMAN_SETMODE_Lie();
			HUMAN_SETMODE_Run();
			goto Follow;
		}
	
		If(!_PlayerInRange(2))
		{
	
			If(!_PlayerInRange(10))
			{
				HUMAN_SETMODE_Stand();
				HUMAN_SETMODE_Run();
				goto Follow;
			}
		
		HUMAN_SETMODE_Crouch();
		HUMAN_SETMODE_Run();
		goto Follow;
		}
	}
}
goto Follow;


Label END:
