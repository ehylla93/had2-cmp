//coop only


block
{
SetAlarmType(1,1); //blizka strelba
SetAlarmType(2,1); //slysim strelbu
SetAlarmType(4,0); //kroky
SetAlarmType(8,0); //zvuky by use
SetAlarmType(16,1); //exploze
SetAlarmType(32,1); //vidi
SetAlarmType(64,1); //jsem zranen
SetAlarmType(128,1); //slysim smrtelny skrek
SetAlarmType(256,1); //pratelska strelba
SetAlarmType(512,1); //vidim flakotu

HUMAN_SETAIMODE_Aggressive();

HUMAN_SETSIGHTRANGE(40);
HUMAN_SETHEARRANGE(100);


integer suspend;
Integer endx;		endx=0;
integer aievent;
integer a;
integer alarmpos;	alarmpos=0;

frame detect1;		FRM_FindFrame (detect1,"detect_zona2");
frame cum1;		FRM_FindFrame(cum1, "z2flak1cum1");
frame cum2;		FRM_FindFrame(cum2, "z2flak1cum2");
Frame z2flakkecani;	FRM_FindFrame(z2flakkecani, "z2flakkecani");
Frame z2_flak2;	FRM_FindFrame(z2_flak2, "z2_flak2");

frame me;		FRM_GetMyFrame(me);
FRM_SwitchFaceTexture(me, "e_f024");
}

//---------------------------------------------------------
if ((_LoadGameValue(3)==0))
{
DisableWhenevers(1);
DisableSignals(1);
HUMAN_Suspend(1);
goto end;
}
//---------------------------------------------------------


HUMAN_Suspend(1);
suspend=1;

goto end;

//---------------------------------------------------------------------------------------------------

Whenever go(_SignalReceived(1))
{
//detektor hracu
HUMAN_Suspend(0);
suspend=0;
SetAlarmType(1023,0);
SetAlarmType(1011,1);	//bez kroku a use
HUMAN_SetAnim("%%nudazed1", 500,500,1);
goto end;
}

Whenever alarm(_SignalReceived(2))
{
HUMAN_SetAnim("", 90, 90, 0);
block
{
SetAlarmType(4,1);
HUMAN_SETSIGHTRANGE(40);
HUMAN_WeaponOnArm(1);	
}	


HUMAN_TurnAtNearestPlayer();
goto end;
}

Whenever vyp(_SignalReceived(3))
{
SetWhenever (alarm,0);
goto end;	
}
//---------------------------------------------------------------------------------------------------------

label hlidejroh:

HUMAN_TurnAt(cum1);
//HUMAN_SetAnim("%%koukadrep", 300, 300, 0);
delay (5000);
HUMAN_TurnAt(cum2);
//HUMAN_SetAnim("%%koukadrep", 300, 300, 0);
delay (5000);

goto hlidejroh;

//----------------------------------------------------------------------------------------



OnAlarm()
{
block
{
DisableWhenevers(1);
DisableSignals(1);
SetAlarmType(1015,1);
aievent=(_GetAlarmType());
PrintfNumber("z2_flak1 alrm:",aievent);
SendSignal (z2flakkecani,1);
SendSignal (z2_flak2,5);
}




if ((((aievent==1) or (aievent==32)) or (aievent==64)) or (endx==1))

	{
	block
	{
	HUMAN_SetAnim("", 100, 100, 0);
	endx=1;
	}
	EndScript();
	}


if (((aievent==2) or (aievent==16)) or (aievent==256))

{


if (_LoadGameValue(6)==0)
	
	{
	HUMAN_SetAlarm(0);
		if (alarmpos==1)
		{
		goto hlidejroh;
		}

	goto end;
	}

if (alarmpos==0)
	{
	SetAlarmType(278,0);
	HUMAN_SetAnim("", 100, 100, 0);
	HUMAN_SetAlarm(0);
	HUMAN_SETMODE_Run();
	HUMAN_WeaponOnArm(1);
	HUMAN_Move("z2_flak1_01");
	block
	{
	alarmpos=1;
	SetAlarmType(278,1);
	}
	
	HUMAN_SETMODE_Crouch();
	goto hlidejroh;
	}
HUMAN_SetAlarm(0);
HUMAN_TurnAtNearestPlayer();
delay (2000);
goto hlidejroh;
}


if (aievent==4)
{

SetAlarmType(4,0);
HUMAN_SetAlarm(0);
HUMAN_TurnAtNearestPlayer();
delay (2000);
SetAlarmType(4,1);

if (alarmpos==1)
	{
	goto hlidejroh;
	}
goto end;
}


block
{
HUMAN_SetAnim("", 100, 100, 0);
endx=1;
}

EndScript();

}


//---------------------------------------------------------------------------------------------------------


OnAlarmDone()
{



SetAlarmType(1023,0);
SetAlarmType(1015,1);
HUMAN_SetAlarm(0);
HUMAN_Reload();
HUMAN_SETMODE_Crouch();
goto end;
}

OnDeath()
{
block
{
DisableWhenevers(1);
DisableSignals(1);
SendSignal (z2flakkecani,1);
}
EndScript();
goto end;
}




label end:
