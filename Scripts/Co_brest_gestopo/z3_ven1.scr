//chodi dokola

block
{
SetAlarmType(1,1); //blizka strelba
SetAlarmType(2,1); //slysim strelbu
SetAlarmType(4,1); //kroky
SetAlarmType(8,0); //zvuky by use
SetAlarmType(16,1); //exploze
SetAlarmType(32,1); //vidi
SetAlarmType(64,1); //jsem zranen
SetAlarmType(128,0); //slysim smrtelny skrek
SetAlarmType(256,1); //pratelska strelba
SetAlarmType(512,1); //vidim flakotu


HUMAN_SETAIMODE_Defensive();
HUMAN_SETMODE_Guard();

HUMAN_SETSIGHTRANGE(30);
HUMAN_SETHEARRANGE(100);

integer a;
integer alarmpos;	alarmpos=0;
integer aievent;
integer endx;		endx=0;
integer suspend;

frame cum1;		FRM_FindFrame(cum1, "z3ven1cum1");
frame cum2;		FRM_FindFrame(cum2, "z3ven1cum2");
frame puf;		FRM_FindFrame(puf, "venek1puf");

frame me;		FRM_GetMyFrame(me);
FRM_SwitchFaceTexture(me, "e_f053");

}



HUMAN_Suspend(1);

goto end;

Whenever go(_SignalReceived(1))
{
//detektor hracu
HUMAN_Suspend(0);
suspend=0;
SetAlarmType(887,1);	
goto chodim;
}

//-------------------------------------------------------------------------------------------------------------

label chodim:

a=1;
HUMAN_Move("z3_ven1_01");
a=2;
HUMAN_SetAnim("%%kouka", 300, 300, 0);
delay (4285);
HUMAN_SetAnim("", 300, 300, 0);
label 2:
HUMAN_Move("z3_ven1_02");
a=3;

label 3:
HUMAN_Move("z3_ven1_03");
a=4;

label 4:
HUMAN_Move("z3_ven1_04");
a=1;
goto chodim;


//---------------------------------------------------------------------------------------------------------


label rozdel:
if (a==1)	{ goto chodim; }
if (a==2)	{ goto 2; }
if (a==3)	{ goto 3; }
if (a==4)	{ goto 4; }


goto end;


//---------------------------------------------------------------------------------------------------------

label hlidejroh:

HUMAN_TurnAt(cum1);
//HUMAN_SetAnim("%%koukadrep", 300, 300, 0);
delay (8000);
HUMAN_TurnAt(cum2);
//HUMAN_SetAnim("%%koukadrep", 300, 300, 0);
delay (8000);

goto hlidejroh;

//---------------------------------------------------------------------------------------------------------

OnAlarm()
{
block
{
DisableWhenevers(1);
DisableSignals(1);
SetAlarmType(512,0);
SetAlarmType(375,1);
aievent=(_GetAlarmType());
PrintfNumber("z3_ven1 alarm c:",aievent);
}
HUMAN_Stop();

if ((((aievent==1) or (aievent==32)) or (aievent==64)) or (endx==1))

	{
	endx=1;
	HUMAN_SetAnim("", 30, 30, 0);
	EndScript();
	}


if (aievent==4)
{

SetAlarmType(4,0);
HUMAN_SetAlarm(0);

HUMAN_Stop();
HUMAN_SetAnim("", 100, 100, 0);
HUMAN_TurnAtNearestPlayer();
delay (2000);
SetAlarmType(4,1);

if (alarmpos==1)
	{
	goto hlidejroh;
	}
goto rozdel;
}



if (((aievent==2) or (aievent==16)) or (aievent==256))
{
HUMAN_Stop();
HUMAN_SetAnim("", 100, 100, 0);

SetAlarmType(278,0);				//strelba a kroky
HUMAN_WeaponOnArm(1);


	if (alarmpos==0)
	{
	HUMAN_SetAlarm(0);
	HUMAN_SETMODE_Run();
	HUMAN_Move("z3_ven1_05");
	block
	{
	alarmpos=1;
	SetAlarmType(278,1);
	}
	
	HUMAN_SETMODE_Crouch();
	}

HUMAN_SetAlarm(0);
SetAlarmType(278,1);				//strelba a kroky
goto hlidejroh;

}

if (aievent==512)
{
SetAlarmType(512,0);
HUMAN_Stop();
HUMAN_WeaponOnArm(1);
HUMAN_Attack(puf, 3000);
endx=1;
EndScript();
}

}

//---------------------------------------------------------------------------------------------------------

OnAlarmDone()
{
SetAlarmType(1023,0);
SetAlarmType(375,1);
HUMAN_Reload();
HUMAN_SetAlarm(0);
HUMAN_SETMODE_Stand();
HUMAN_Move("z3_ven1_05");
HUMAN_SETMODE_Crouch();
goto hlidejroh;
}

OnDeath()
{
DisableWhenevers(1);
DisableSignals(1);
EndScript();
goto end;
}




label end:




//chodi dokola .. na kroky se podiva na  nejblisiho a rozhlidne...
// na strelbu pokud neni hrac in range bezi na roh, cupne a hlida.
//		       je in r. tak endscript